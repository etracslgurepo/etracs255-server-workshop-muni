import com.rameses.annotations.*;
import com.rameses.common.*;
import com.rameses.util.*;
import java.rmi.server.UID;

class QueueService { 

    @XConnection('queue_ws_server')
    def wssender;

	@DataContext(value='queue_group', adapter='queue') 
	def qg; 

	@DataContext(value='queue_counter', adapter='queue') 
	def qc; 

	@DataContext(value='queue_section', adapter='queue') 
	def qs; 

	@DataContext(value='queue_number', adapter='queue') 
	def qn; 

	@DataContext(value='queue_number_archive', adapter='queue') 
	def qna; 	

	@DataContext(value='queue_number_counter', adapter='queue') 
	def qnc; 

	@Service('DateService')
	def dateSvc;

	@Service 
	def self; 

	def dateFormatter = new java.text.SimpleDateFormat('MMMMM dd, yyyy'); 


	/* initiated by taxpayer to get the next current series number  */
	@ProxyMethod 
	public def fetchNextNumber( o ) {
		def dd = fetchNextTicket( o ); 
		return dd.ticketno; 
	} 

	@ProxyMethod 
	public def fetchNextTicket( o ) {
		if ( !o.sectionid ) throw new Exception('sectionid parameter is required');

		qs.find([ objid: o.sectionid ]).update([ lockid: new UID().toString() ]); 

		def m = qs.find([ objid: o.sectionid ]).first(); 
		if ( !m ) throw new Exception('queue section does not exist'); 

		def prefix = m.prefix; 
		def nextseries = m.currentseries+1; 
		qs.find([ objid: o.sectionid ]).update([ currentseries: nextseries ]); 

		def dd = [ 
			txndate: dateSvc.getServerDate(), 
			groupid: m.groupid, sectionid: m.objid, 
			seriesno: nextseries, state : 'PENDING' 
		]; 
		dd.ticketno = formatSeriesNumber([ seriesno: dd.seriesno, prefix: prefix ]); 
		qn.create( dd ); 
		dd.group = m.group; 
		dd.section = [ objid: m.objid, title: m.title ]; 
		dd.txndatestr = dateFormatter.format( dd.txndate );  
		return dd; 
	} 

	@ProxyMethod
	def formatSeriesNumber( data ) {
		if ( data.seriesno ) {
			def prefix = (data.prefix? data.prefix : ''); 
			def snum = data.seriesno.toString();
			if ( snum.length() < 3 ) { 
				snum = snum.padLeft(3,'0'); 
			} 
			return ''+prefix+snum; 
		} else { 
			return ''; 
		} 
	} 

	@ProxyMethod 
	public def getWaitingList( params ) { 
		if ( !params._start ) params._start = 0; 
		if ( !params._limit ) params._limit = 20;

		def rundate = dateSvc.getServerDate(); 
		def startdate = new java.sql.Date( rundate.getTime()).toString(); 
		def enddate = startdate +' 23:59:59'; 

		def m = [state: 'PENDING', startdate: startdate, enddate: enddate]; 
		m._start = params._start; 
		m._limit = params._limit; 

		if ( params.counterid ) { 
			m.counterid = params.counterid; 
			return qn.getPendingListByCounter( m ); 
		} else if ( params.groupid ) {
			m.groupid = params.groupid; 
			return qn.getPendingListByGroup( m ); 
		} else {
			return []; 
		} 
	}

	@ProxyMethod 
	public def getPendingSections( params ) { 
		return qn.getPendingSections( params ); 
	} 
} 