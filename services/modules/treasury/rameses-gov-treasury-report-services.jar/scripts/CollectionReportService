import com.rameses.annotations.*;
import com.rameses.common.*
import com.rameses.util.*;

class CollectionReportService  {

	@ActiveDB('collection_report')
	def adb; 

	@DataContext(dynamic=true)
	def em;

	@Env
	def env; 

	@Service('DateService')
	def dtSvc; 

	@ProxyMethod 
	public def initReport( param ) {
		def rundate = new java.sql.Date( dtSvc.getServerDate().getTime()); 
		def arrs = rundate.toString().split("-"); 
		def resp = [ 
			funds  : [], 
			months : dtSvc.getMonths(), 
			year   : arrs[0].toInteger(), 
			month  : arrs[1].toInteger(),  
			day    : arrs[2].toInteger() 
		]; 

		def srcfunds = adb.getFunds([ filter: '' ]); 
        def groups = srcfunds.collect{[ objid: it.groupid, indexno: it.groupindexno ]}.unique();  
        groups.sort{ it.indexno }.each{ g-> 
            resp.funds << g;
            g.category = 'group'; 
            g.desc = g.objid + ' GROUP'; 
            
            srcfunds.findAll{( it.groupid == g.objid )}.each{ f-> 
                resp.funds << f; 
                f.category = 'item'; 
                f.desc = '    '+ f.title;
            } 
        } 

        if ( param?.includeUsers.toString().matches('1|true')) {
        	def em_ur = em.lookup('sys_user_role'); 
        	resp.users = em_ur.find([ domain: 'TREASURY', role: 'COLLECTOR' ]).orderBy('name,orgid').list();
        }

        if ( env.USERID ) { 
        	def em_ur = em.lookup('sys_user_role'); 
        	resp.user = em_ur.find([ objid: env.USERID, domain: 'TREASURY', role: 'COLLECTOR' ]).orderBy('name,orgid').first(); 
        } 
		return resp; 
	}

	def resolveDate( value ) {
		if ( !value ) return null; 
		if ( value instanceof java.util.Date ) return value; 

		try {
			return java.sql.Date.valueOf( value.toString()); 
		} catch(Throwable t) {;}

		try {
			def o = java.sql.Timestamp.valueOf( value.toString()); 
			return new java.sql.Date( o.time ); 
		} catch(Throwable t) {;}
	}

	@ProxyMethod
	public def getReport( params ) { 
		if ( !params.period?.type )
			throw new Exception('period.type  parameter is required'); 
		if ( !params.startdate ) 
			throw new Exception('startdate  parameter is required'); 
		if ( !params.enddate ) 
			throw new Exception('enddate  parameter is required'); 

		def startdate = resolveDate( params.startdate ); 
		if ( !startdate ) throw new Exception('Invalid date value for startdate'); 

		def enddate = resolveDate( params.enddate ); 
		if ( !enddate ) throw new Exception('Invalid date value for enddate'); 

		def YMD = new java.text.SimpleDateFormat("yyyy-MM-dd");
		params.enddate = new DateBean( enddate ).add("1d"); 
		params.enddate = new java.sql.Date( params.enddate.time ); 
		params.startdate = startdate; 

		if ( params.postingtype == null ) 
			params.postingtype = [:]; 

		if ( !params.postingtype.objid ) 
			params.postingtype.objid = 'BY_LIQUIDATION_DATE'; 

		if ( !params.postingtype.title ) 
			params.postingtype.title = params.postingtype.objid; 

		def filterBuff = new StringBuilder();
		if ( params.fund?.objid ) { 
			params.fundid = params.fund.objid; 
			if ( params.fund.category == 'group' ) { 
				filterBuff.append(''' AND fund.groupid = $P{fundid} '''); 
			} 
			else {
				filterBuff.append(''' AND fund.objid = $P{fundid} '''); 
			}
		}  

		if ( params.tag == 'collector' ) {
			if ( !params.collector?.objid ) throw new Exception('Select a Collector first'); 
			if ( !params.collector?.name ) throw new Exception('collector.name  parameter is required'); 
		}

		def collectorname = '( ALL )'; 
		if ( params.collector?.objid ) { 
			collectorname = params.collector.desc; 
			if ( !collectorname ) collectorname = params.collector.name;  

			params.collectorid = params.collector.objid 
			filterBuff.append(' AND t0.collectorid = $P{collectorid} '); 
		}

		params.filter = filterBuff.toString(); 

		def items = null; 
		if ( params.postingtype.objid == 'BY_REMITTANCE_DATE' ) { 
			items = adb.getRemittedReport( params ); 
		} 
		else { 
			items = adb.getLiquidatedReport( params ); 
		} 
		
		if ( !items ) throw new Exception('No available record(s) found that matches your criteria'); 

		def periodtype = params.period.type;
		if ( !params.period.title ) 
			params.period.title = periodtype.toUpperCase();  

		return [
			header : [
				REPORTTITLE     : 'DAILY REPORT OF COLLECTION LISTING', 
				PREPAREDBY 	    : env.FULLNAME, 
				PREPAREDBYTITLE : env.JOBTITLE, 
				POSTINGTYPE     : params.postingtype.title.toUpperCase(), 
				COLLECTORNAME	: '   '+ (collectorname ? collectorname : ''), 
				PERIODTYPE      : periodtype.toUpperCase(), 
				PERIOD          : buildPeriodTitle( periodtype, startdate, enddate ) 
			], 
			reportdata: [ receipts : items ]  
		]; 
	}


	@ProxyMethod
	public def getReportSummary( params ) { 
		if ( !params.period?.type )
			throw new Exception('period.type  parameter is required'); 
		if ( !params.startdate ) 
			throw new Exception('startdate  parameter is required'); 
		if ( !params.enddate ) 
			throw new Exception('enddate  parameter is required'); 

		def startdate = resolveDate( params.startdate ); 
		if ( !startdate ) throw new Exception('Invalid date value for startdate'); 

		// this will only support 'monthly' period type 
		def enddate = new DateBean( startdate ).getMonthEnd(); 
		enddate = new java.sql.Date( enddate.time ); 
		startdate = new java.sql.Date( startdate.time ); 

		params.enddate = new DateBean( enddate ).add("1d"); 
		params.enddate = new java.sql.Date( params.enddate.time ); 
		params.startdate = startdate; 

		if ( params.postingtype == null ) 
			params.postingtype = [:]; 

		if ( !params.postingtype.objid ) 
			params.postingtype.objid = 'BY_LIQUIDATION_DATE'; 

		if ( !params.postingtype.title ) 
			params.postingtype.title = params.postingtype.objid; 

		if ( params.tag == 'collector' ) {
			if ( !params.collector?.objid ) throw new Exception('Select a Collector first'); 
			if ( !params.collector?.name ) throw new Exception('collector.name  parameter is required'); 
		}

		def collectorname = null; 
		if ( params.collector?.objid ) { 
			collectorname = params.collector.desc; 
			if ( !collectorname ) collectorname = params.collector.name;  

			params.collectorid = params.collector.objid 
		}
		else {
			params.collectorid = '%'; 
		}

		params.postingtypeid = params.postingtype.objid; 

		def funds = adb.getFundSummary( params ); 
		if ( !funds ) throw new Exception('No available record(s) found that matches your criteria'); 

		def subqry = [];
		def sqlqry = [];
		def header = [:];
		def colindex = 0;
		funds.each {
			subqry << "( select SUM(amount) from remittance_fund where objid = rf.objid and fund_objid='${it.fundid}' ) as col${colindex}";
			sqlqry << " sum( col${colindex} ) as col${colindex} ";
			header['COL'+ colindex] = it.title;
			colindex++; 
		} 
		params.subqry = subqry.join(", ");
		params.sqlqry = sqlqry.join(", ");

		def items = adb.getReportSummary( params ); 
		if ( !items ) throw new Exception('No available record(s) found that matches your criteria'); 

		def dtbean = new DateBean( startdate ); 
		while ( enddate.after( dtbean.date )) {
			def day = dtbean.day; 
			def itm = items.find{ it.xdate == day } 
			if ( !itm ) items << [ xdate: day ]; 

			dtbean.add("1d"); 
		}
		items.sort{ ((Number) it.xdate).intValue() } 

		def periodtype = params.period.type;
		if ( !params.period.title ) 
			params.period.title = periodtype.toUpperCase();  

		header.putAll([
			REPORTTITLE      : 'DAILY REPORT OF COLLECTION SUMMARY BY FUND', 
			PREPAREDBY_NAME  : env.FULLNAME, 
			PREPAREDBY_TITLE : env.JOBTITLE, 
			PERIODTYPE       : periodtype.toUpperCase(), 
			PERIOD           : buildPeriodTitle( periodtype, startdate, enddate ), 

			POSTINGTYPE_FILTER : params.postingtype.title.toUpperCase(), 
			COLLECTOR_FILTER   : ( params.collectorid == '%' ? 'ALL COLLECTORS' : collectorname )
		]); 
		return [ header: header, reportdata: items ]; 
	}


	def buildPeriodTitle( periodtype, startdate, enddate ) {
		if ( periodtype == 'yearly' ) {
			def formatter = new java.text.SimpleDateFormat("yyyy"); 
			return 'FOR THE YEAR '+ formatter.format( startdate ); 
		} 
		else if ( periodtype=='quarterly' ) {
			def arr = new java.text.SimpleDateFormat("MM-yyyy").format( startdate ).split("-");
			def qtr = arr[0].toInteger(); 
			return 'FOR THE QUARTER '+ qtr +', '+ arr[1]; 
		} 
		else if ( periodtype=='monthly' ) {
			def formatter = new java.text.SimpleDateFormat("MMMMM yyyy"); 
			return formatter.format( startdate ).toUpperCase(); 
		} 
		else if ( periodtype == 'daily' ) {
			def formatter = new java.text.SimpleDateFormat("MMMMM dd, yyyy"); 
			return formatter.format( startdate ).toUpperCase(); 
		} 
		else if ( periodtype == 'range' ) {
			if ( startdate == enddate ) {
				def formatter = new java.text.SimpleDateFormat("MMMMM dd, yyyy"); 
				return formatter.format( startdate ).toUpperCase()
			}
			else {
				def formatter = new java.text.SimpleDateFormat("MMM/dd/yyyy"); 
				def buff = new StringBuilder(); 
				buff.append( formatter.format( startdate ).toUpperCase()).append(" - "); 
				buff.append( formatter.format( enddate ).toUpperCase()); 
				return buff.toString(); 
			}
		} 
		return null; 
	}
}
