import com.rameses.annotations.*;

class BillingCashReceiptService {

	@Service('BillingInfoHandler')
	def handler;

	@Service("BillingRuleService")
	def ruleSvc;	

	@Service("PaymentPostingService")
	def pmtSvc;

	@Service("PaymentPostingHandler")
	def pmtHandler;

	@ProxyMethod
	public def getInfo( pp ) { 
		if ( pp.options == null ) pp.options = [:];
		if( !pp.rulename ) throw new Exception("BillingCashReceiptService error. rulename is required");
		if( !pp.params ) throw new Exception("BillingCashReceiptService error. params is required");

		//this is used as an option because in vehicle reg, getting billitems has a different query than getting for cashreceipt
		pp.options.request_action = "cashreceipt";

		boolean returnsCredit = (pp.options.returns_credit.toString() == 'true'); 
		boolean includeCredit = (pp.options.include_credit.toString() == 'false' ? false : true ); 

		pp.options.include_items = true; 

		pp.headerinfo = handler.getHeaderInfo( pp ); 
		pp.billitems = handler.getBillItems( pp ); 

		def citems = handler.getCreditItems( pp ); 
		citems.each{ it.txntype = 'creditpayment' } 

		if ( includeCredit ) { 
			if ( citems && pp.billitems ) { 
				pp.billitems.addAll( citems ); 
			} 
		}

		def m = [:]; 
		m.putAll( pp.params ); 
		m.putAll( pp.headerinfo ); 
		m.billitems = pp.billitems; 

		def ruleParam = [ rulename: pp.rulename ]; 
		ruleParam.putAll( pp.options ); 
		ruleParam.params = m; 

		def warning = null; 
		try { 
			handler.beforeExecute( ruleParam ); 
		} catch (com.rameses.util.Warning w) { 
			warning = w; 
		} 

		def ruleRes = ruleSvc.execute( ruleParam ); 
		//if ( !ruleRes.items ) throw new Exception('There must be a result items in your BillingRuleService.execute'); 

		m = [:];
		m.putAll( pp.headerinfo );  
		m.putAll( ruleRes );  
		m.rulename = pp.rulename; 
		m.txntype = pp.txntype; 
		if( !m.txntype ) m.txntype = pp.params?.collectiontype?.handler;

		handler.validateReceiptInfo( m );

		def bill = handler.formatReceiptInfo( m ); 
		if ( returnsCredit ) bill.creditpayments = citems; 

		bill.billitems = m.billitems;
		bill.items = m.items; 
		bill.amount = m.items.sum{( it.amount ? it.amount : 0.0 )} 
		bill.remove('objid');	//we should remove objid bec. there might be conflicts 
		bill._postpayment = true; //we should mark this to indicate posting will be automatically handled
		bill._warning = warning; 
		return bill; 
	} 

	@ProxyMethod
	public def post( def e ) {
		if ( !e.txntype ) throw new Exception('txntype parameter is required');

		def payinfo = pmtHandler.getPaymentInfo( e ); 
		payinfo.putAll( e );
		if ( !payinfo._schemaname ) throw new Exception("payinfo._schemaname is required");
		if ( !payinfo.items ) throw new Exception('payinfo.items is required'); 

		payinfo._workflow = pmtHandler.getPaymentWorkflow( e ); 
		pmtSvc.post( payinfo );
	} 



}