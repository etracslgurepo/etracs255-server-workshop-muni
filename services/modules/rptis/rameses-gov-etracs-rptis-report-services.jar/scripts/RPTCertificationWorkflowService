import com.rameses.annotations.*
import com.rameses.common.*
import com.rameses.util.*
import com.rameses.services.extended.*


class RPTCertificationWorkflowService extends ActiveWorkflowService
{
	@Service('DateService')
	def dtSvc

	@ActiveDB('rptcertification_task')
	def taskDB

	@Service('NotificationService')
	def notificationService

	@Service('RPTCertificationService')
	def certSvc


	@DataContext('rptcertification')
	def em_certification

	@DataContext('rptcertification_task')
	def em_certification_task

	String processname = 'rptcertification'


	public void beforeCreateTask(task) {
		def assignee = [objid:env.USERID, name:env.FULLNAME, title:env.JOBTITLE]
		if (task.assignee) {
			assignee = task.assignee
			env.target = [
				assignee: assignee,
				refid: task.refid,
				sender: task.assignee
			] 
		}

		if ( env.action?.matches('return.*')){
			def returnto = findReturnToInfo(task)
			if (!returnto) 
				throw new Exception('This document did not pass from ' + task.state + '.\nReturn task to another personnel.')

			env.returnto = returnto.assignee
			task.assignee = assignee
			task.returnedby = env.prevtask.state
		}
		else if ( ! task.state.toLowerCase().matches('assign.*')){
			task.assignee = assignee
		}
	}

	public void afterCreateTask(task){
		def msg = '';
		if (task.state.matches('assign.*'))
			msg = 'Certification is already submitted ' + task.title + '.'
		else
			msg = 'Certification is currently for ' + task.title + ' by ' + task.assignee.name + '.'

		if (env.target) {
			env.target.taskid = task.objid 
			env.target.txnno = env.data.txnno
		}

		em_certification.find([objid: task.refid]).update([taskid: task.objid])
		if (task.state == 'released') {
			em_certification_task.find([objid: task.objid]).update([assignee_objid: '{NULL}'])
		}
	}
	

	public void afterOpenTask(task) {
		if (task.state.matches('assign.*')){
			task.owner = true 
		}

		task.data = certSvc.openCertification([objid:task.refid])
		removeUserNotification(task)
		env.task = task 
	}

	public void afterLoadTask(Object newTask) {
		newTask.transitions.findAll{ it.action.toString().matches('approve.*|submit.*|send.*')}.each{t ->
			if (t.properties?.targetrole) {
				def params = [role: t.properties.targetrole]
				t.assignees = taskDB.getTargetAssignees(params)
			}
		}
	}

	public void afterSignal(Object result) {
		if (result && result.task && !result.data)
			result.data = certSvc.openCertification([objid:result.task.refid])
	}

	void removeUserNotification(task){
		if (task.assignee?.objid == env.USERID )
			removeNotification(task)
	}

	def getNotificationMessage(task){
		if (task?.state?.matches('assign.*')){
			return notifyGroup(task)
		}
		else if (env.action?.matches('return.*')){
			return notifyAssignee(task)
		}
		else if (env.prevtask?.state.matches('assign.*')){
			removeNotification(task)
		}
		else if (env.target) {
			return notifyTargetAssignee()	
		}
	}


	void removeNotification(task) {
		if (task){
        	notificationService.removeMessage([objid:task.refid])
		}
	}


	def notifyGroup(task){
		def msg = 'Certification issuance ' + ' (' + task.state + ')'
        return [
            objid     		: task.refid,
            senderid  		: env.prevtask?.actor?.objid,
            sender    		: env.prevtask?.actor?.name,
            recipientid   	: task.state,
            recipienttype 	: 'group',
            message   		: msg,
            filetype  		: 'rptcertification',
            data 	  		: [taskid: task.objid],
        ]
	}

	def notifyAssignee( task ) {
		def certification = env.data 
		taskDB.updateTaskAssignee([
			objid 			: task.objid, 
			assigneeid 		: env.returnto.objid,
			assigneename 	: env.returnto.name,
			assigneetitle 	: env.returnto.title
		])

        return [
            objid     : task.refid,
            recipientid : (env.returnto ? env.returnto.objid : task.assignee.objid),
            recipienttype : 'user',
            senderid  : task.assignee.objid,
            sender    : task.assignee.name,
            message   : 'Certification ' + certification.txnno + '   was resent by ' + env.prevtask?.actor?.name,
            filetype  : 'rptcertification',
            data 	  : [taskid: task.objid],
        ]
	}

	def notifyTargetAssignee() {
		def target = env.target 
        return [
            objid     : target.refid,
            recipientid : target.assignee?.objid,
            recipienttype : 'user',
            senderid  : env.prevtask?.actor?.objid,
            sender    : env.prevtask?.actor?.name,
            message   : 'Certification ' + target.txnno + '   was submitted by ' + env.prevtask?.actor?.name,
            filetype  : 'rptcertification',
            data 	  : [taskid: target.taskid],
        ]
	}

	@ProxyMethod
	public def findReturnToInfo( param ) {
		return taskDB.findReturnToInfo(param)	
	}
}

