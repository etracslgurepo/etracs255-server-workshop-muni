import com.rameses.annotations.*
import com.rameses.common.*;
import com.rameses.services.extended.*;

class RPTReportComparativeAVService
{
	@Env 
	def env 

    @ActiveDB('rptreport_comparativeav')
    def em

    @Service('Var')
    def var 

    @Service('RPTUtil')
    def util

    @DataContext('propertyclassification')
    def propertyclassification 

    @DataContext('exemptiontype')
    def exemptiontype

    @Async
	@ProxyMethod
	public Map generateComparativeAV(params) {
		params.filter = ''

		if (params.ry){
			params.filter += ' AND rp.ry =  $P{ry} ' 
		}

		if (params.lgu){
			params.filter += ' AND f.lguid =  $P{lguid} ' 
			params.lguid = params.lgu.objid 
		}

		if (params.barangay){
			params.filter += ' AND rp.barangayid = $P{barangayid} '
			params.barangayid = params.barangay.objid 
		}


		util.buildReportFormalTitlePeriod('COMPARATIVE DATA ON ASSESSED VALUE OF REAL PROPERTY', params)
		util.buildStartEndDateFilter(params)

		def taxablepreceedings 	= em.getPreceedingComparativeAV(params)
		def exemptpreceedings 	= em.getPreceedingComparativeAVExempt(params)
		def taxablecurrents 	= em.getCurrentComparativeAV(params)
		def exemptcurrents 		= em.getCurrentComparativeAVExempt(params)
		def taxablecancelleds 	= em.getCancelledComparativeAV(params)
		def exemptcancelleds 	= em.getCancelledComparativeAVExempt(params)
		
		def loadSumaryInfo = { data, list ->
			def info = list.find{ it.classid == data.classid }
			if( info ) data.putAll( info )
		}
	
		int lineno = 1
		int charindex = 'a' 
		def classifications = getClassifications()
		def firstspecial = classifications.find{ it.special == 1 }
		classifications.add( classifications.indexOf( firstspecial), [objid:'SPECIAL', name:'SPECIAL', special:0] )
		def taxables = classifications.collect{ buildComparativeInfo(it, 'TAXABLE') }
		taxables.each { data ->
			loadSumaryInfo( data, taxablepreceedings )
			loadSumaryInfo( data, taxablecurrents )
			loadSumaryInfo( data, taxablecancelleds )
			if( data.special == 0 ) {
				data.classname = lineno + '. ' + data.classname 
				lineno += 1
			}
			else {
				data.classname = '   ' + ((char)charindex) + '. ' + data.classname 
				charindex += 1
			}
		}
		buildEndings(taxables)
		
		lineno = 1 
		def exempts  = getExemptionTypes().collect{ buildComparativeInfo(it, 'EXEMPT') }
		exempts.each { data -> 
			loadSumaryInfo( data, exemptpreceedings )
			loadSumaryInfo( data, exemptcurrents )
			loadSumaryInfo( data, exemptcancelleds )
			data.classname = lineno + '. ' + data.classname 
			lineno += 1
		}
		buildEndings(exempts)
		
		def forlguname = env.ORGCLASS +  ' OF ' + env.ORGNAME 
		if (params.lgu){
			forlguname = params.lgu.lgutype.toUpperCase() + ' OF ' + params.lgu.name
		}

		def p = [
			PERIOD 		: params.period,
			PERIODTYPE 	: params.periodtype,
			TITLE 		: params.title,
			FORLGUNAME  : forlguname,
		]
		return [
			parameters  : p,
			reportdata  : taxables + exempts ,
		]
	}


	def buildEndings(data){
		data.each{
			it.endinglandav = it.preceedinglandav + it.currentlandav - it.cancelledlandav
			it.endingimpav = it.preceedingimpav + it.currentimpav - it.cancelledimpav
			it.endingtotal = it.preceedingtotal + it.currenttotal - it.cancelledtotal
		}
	}

	def buildComparativeInfo( info, taxability ) {
		return [ classid	: info.objid, 
				classname	: info.name, 
				special		: info.special ,
				taxability	: taxability,
				preceedinglandav 	: 0.0,
				preceedingimpav 	: 0.0,
				preceedingtotal 	: 0.0,
				currentlandav 	: 0.0,
				currentimpav 	: 0.0,
				currenttotal 	: 0.0,
				cancelledlandav : 0.0,
				cancelledimpav 	: 0.0,
				cancelledtotal 	: 0.0,
				endinglandav 	: 0.0,
				endingimpav 	: 0.0,
				endingtotal 	: 0.0,
			]
	}


	@ProxyMethod
    public def getClassifications(){
    	return propertyclassification.find([state:'APPROVED']).orderBy('orderno').list()
    }

    
    @ProxyMethod
    public def getExemptionTypes(  ) {
    	return 	exemptiontype.find([state:'APPROVED']).orderBy('orderno').list()
    }

}
